import kotlinx.kover.gradle.aggregation.settings.dsl.minBound

rootProject.name = "Outpost"

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

pluginManagement {
    repositories {
        google {
            mavenContent {
                includeGroupAndSubgroups("androidx")
                includeGroupAndSubgroups("com.android")
                includeGroupAndSubgroups("com.google")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositories {
        google {
            mavenContent {
                includeGroupAndSubgroups("androidx")
                includeGroupAndSubgroups("com.android")
                includeGroupAndSubgroups("com.google")
            }
        }
        mavenCentral()
    }
}

include(":kotlin-outpost:android")
include(":kotlin-outpost:desktop")
include(":kotlin-outpost:shared")
include(":outpost-core")

plugins {
    id("org.jetbrains.kotlinx.kover.aggregation") version "0.9.10"
    id("org.gradle.toolchains.foojay-resolver-convention") version "1.0.0"
}

kover {
    enableCoverage()
    reports {
        excludedClasses = listOf(
            // Untestable framework code
            "MainKt",
            "ComposableSingletons*",
            "*.ComposableSingletons*",
            "*.AppActivity",
            "*.generated.resources.*"
        )

        verify {
            rule {
                name = "Minimum coverage"
                minBound(80)
            }
        }
    }
}
